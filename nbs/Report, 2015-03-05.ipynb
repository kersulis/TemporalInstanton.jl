{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The multi-time-step instanton problem has the following components:\n",
    "\n",
    "1. Quadratic objective function of deviation variables\n",
    "2. Linear power balance constraints\n",
    "3. Quadratic line temperature constraint\n",
    "\n",
    "Here I describe each component separately. I also include code that may be used to generate each component.\n",
    "___"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 1. Objective Function\n",
    "\n",
    "The objective function weights only the deviation variables $x$. For two time steps, we have:\n",
    "\n",
    "\\begin{align}\n",
    "\\min & \\begin{bmatrix} x^{(1)\\top} & \\theta^{(1)\\top} & \\alpha^{(1)} & x^{(2)\\top} & \\theta^{(2)\\top} & \\alpha^{(2)} \\end{bmatrix}\n",
    "\\underbrace{\n",
    "\\begin{bmatrix} Q_x & 0 & 0 & 0 & 0 & 0 \\\\ 0 & 0 & 0 & 0 & 0 & 0 \\\\ 0 & 0  & 0 & 0 & 0 & 0 \\\\ 0 & 0 & 0 & Q_x & 0 & 0 \\\\ 0 & 0 & 0 & 0 & 0 & 0 \\\\ 0 & 0 & 0 & 0 & 0 & 0\\end{bmatrix}}_{Q_{obj}}\n",
    "\\begin{bmatrix} x^{(1)} \\\\ \\theta^{(1)} \\\\ \\alpha^{(1)} \\\\ x^{(2)} \\\\ \\theta^{(2)} \\\\ \\alpha^{(2)} \\end{bmatrix} \\\\\n",
    "&= \\sum_{t=1}^{T} x^{(t)\\top}Q_xx^{(t)}\n",
    "\\end{align}\n",
    "\n",
    "### Computing the objective function\n",
    "\n",
    "The following method generates the matrix $Q_{obj}\\in\\mathbb{R}^{2nT\\times 2nT}$, where each $Q_x$ is the identity matrix (no correlation):"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "function tmp_inst_Qobj(n,nr,T)\n",
    "    \"\"\" Generate the objective function matrix\n",
    "    Qobj from the problem dimensions.\n",
    "    Assume no correlation between wind sites.\n",
    "    \"\"\"\n",
    "    return sparse(diagm(repeat([ones(nr),zeros(n)],outer=[T])))\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 2. Power Balance Constraints\n",
    "\n",
    "Suppose our network has three nodes. Nodes 1 and 2 have wind farms, and node 3 is the angle reference. After removing the third row and column, our admittance matrix is\n",
    "\n",
    "$$ Y = \\begin{bmatrix} y_{11} & y_{12} \\\\ y_{12} & y_{22} \\end{bmatrix}. $$\n",
    "\n",
    "Assuming there are two time steps (just enough to see the pattern), the vector of variables is\n",
    "\n",
    "\\begin{align}\n",
    "z &= \\begin{bmatrix} z^{(1)} \\\\ z^{(2)} \\end{bmatrix} \\text{, where} \\\\\n",
    "z^{(1)} &= \\begin{bmatrix} x_1^{(1)} & x_2^{(1)} & x_3^{(1)} & \\theta_1^{(1)} & \\theta_2^{(1)} & \\alpha^{(1)} \\end{bmatrix}^\\top \\\\\n",
    "z^{(2)} &= \\begin{bmatrix} x_1^{(2)} & x_2^{(2)} & x_3^{(2)} & \\theta_1^{(2)} & \\theta_2^{(2)} & \\alpha^{(2)} \\end{bmatrix}^\\top\n",
    "\\end{align}\n",
    "There are four linear equations per time step describing power balance and distributed slack. Let's consider the four equations of the first time step. The first describes $\\alpha$, the mismatch variable necessary to implement distributed slack. \n",
    "\n",
    "\\begin{align}\n",
    "\\alpha^{(1)} &= 1^\\top(D^{(1)} - x^{(1)} - G^{0(1)}) \\\\\n",
    "&= (D_1^{(1)} + D_2^{(1)} + D_3^{(1)}) - (x_1^{(1)} + x_2^{(1)} + x_3^{(1)}) - (G_1^{0(1)} + G_2^{0(1)} + G_3^{0(1)})\n",
    "\\end{align}\n",
    "Distributed slack uses a vector of generator participation factors to assign a pre-defined portion of the power mistmatch to each generator. We can express the actual generation at a time step as a function of the dispatch values $G^0$ and the mismatch $\\alpha$:\n",
    "\n",
    "\\begin{equation}\n",
    "G_i^{(1)} = G_i^{0(1)} + \\alpha^{(1)} k_i\n",
    "\\end{equation}\n",
    "Intuitively, this means that when demand exceeds total generation, $\\alpha$ will be positive, and each generator $G_i$ will increase its output by a fraction $k_i$ of $\\alpha$, thereby balancing the mismatch.\n",
    "\n",
    "Having described the behavior of conventional generators, we are ready to relate the bus angles to power injections at nodes 1 and 2:\n",
    "\n",
    "\\begin{align}\n",
    "y_{11}\\theta_1^{(1)} + y_{12}\\theta_2^{(1)} - x_1^{(1)} &= G_1^{(1)} - D_1^{(1)} \\\\\n",
    "&= (G_1^{0(1)} + \\alpha^{(1)} k_1) - D_1^{(1)} \\\\\n",
    "\\implies \\begin{bmatrix} -1 & 0 & 0 & y_{11} & y_{12} & -k_1 \\end{bmatrix} z^{(1)} &= G_1^{0(1)} - D_1^{(1)} \\\\\n",
    "\\\\\n",
    "y_{12}\\theta_1^{(1)} + y_{22}\\theta_2^{(1)} - x_2^{(1)} &= G_2^{(1)} - D_2^{(1)} \\\\\n",
    "&= (G_2^{0(1)} + \\alpha^{(1)} k_2) - D_2^{(1)} \\\\\n",
    "\\implies \\begin{bmatrix} 0 & -1 & 0 & y_{12} & y_{22} & -k_2 \\end{bmatrix} z^{(1)} &= G_2^{0(1)} - D_2^{(1)} \\\\\n",
    "\\end{align}\n",
    "At node 3 we know the angle is zero, but we still must express the relationship between wind deviation and conventional generation:\n",
    "\n",
    "\\begin{align}\n",
    "-x_3^{(1)} - \\alpha^{(1)}k_3 &= G_3^{0(1)} - D_3^{(1)} \\\\\n",
    "\\implies \\begin{bmatrix} 0 & 0 & -1 & 0 & 0 & -k_3 \\end{bmatrix} z^{(1)} &= G_3^{0(1)} - D_3^{(1)}\n",
    "\\end{align}\n",
    "We can rewrite the equation for $\\alpha$ in inner product form:\n",
    "\n",
    "\\begin{align}\n",
    "\\begin{bmatrix} 1 & 1 & 1 & 0 & 0 & 1 \\end{bmatrix} z^{(1)} &= \\sum_{i=1}^3 D_i^{(1)} -G_i^{0(1)}\n",
    "\\end{align}\n",
    "\n",
    "___"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now let's put everything together. The four power balance equations may be written as a single linear equation in $z^{(1)}$:\n",
    "\n",
    "\\begin{align}\n",
    "\\begin{bmatrix}\n",
    "-1 & 0 & 0 & y_{11} & y_{12} & -k_1 \\\\\n",
    "0 & -1 & 0 & y_{12} & y_{22} & -k_2 \\\\\n",
    "0 & 0 & -1 & 0 & 0 & -k_3 \\\\\n",
    "1 & 1 & 1 & 0 & 0 & 1 \\\\\n",
    "\\end{bmatrix}\n",
    "\\begin{bmatrix} x_1^{(1)} \\\\ x_2^{(1)} \\\\ x_3^{(1)} \\\\ \\theta_1^{(1)} \\\\ \\theta_2^{(1)} \\\\ \\alpha^{(1)} \\end{bmatrix} &=\n",
    "\\begin{bmatrix}\n",
    "G_1^{0(1)} - D_1^{(1)} \\\\\n",
    "G_2^{0(1)} - D_2^{(1)} \\\\\n",
    "G_3^{0(1)} - D_3^{(1)} \\\\\n",
    "\\sum_{i=1}^3 D_i^{(1)} -G_i^{0(1)}\n",
    "\\end{bmatrix}\n",
    "\\end{align}\n",
    "Generalizing, we find that \n",
    "\n",
    "* There are $(n+n_r)T$ variables --where $n$ is the number of nodes, $n_r$ is the number of nodes with wind farms, and $T$ is the number of time steps\n",
    "    * $n_rT$ of these are deviation variables,\n",
    "    * $(n-1)T$ are angle variables, and \n",
    "    * $T$ are mismatch variables.\n",
    "\n",
    "* There are $(n+1)T$  equations, where \n",
    "    * $nT$ describe injections and \n",
    "    * $T$ describe mismatches.\n",
    "\n",
    "* Thus, the power balance equations are of the form $Az = b$, where\n",
    "    * $A\\in\\mathbb{R}^{(n+1)T\\times (n+n_r)T}$\n",
    "    * $z\\in\\mathbb{R}^{(n+n_r)T\\times 1}$\n",
    "    * $b\\in\\mathbb{R}^{(n+1)T\\times 1}$\n",
    "\n",
    "### Computing power balance constraint parameters\n",
    "\n",
    "The following methods generate $A$ and $b$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "function tmp_inst_A(n,Ridx,T,Y,slack,k)\n",
    "    \"\"\" Generate the power balance constraint A matrix\n",
    "    from problem dimensions, admittance matrix,\n",
    "    and generator participation factors.\n",
    "    Assumes the admittance matrix is n-by-n.\n",
    "    \n",
    "    Returns A, which is (n+1)*T-by-(n+nr)*T\n",
    "    \n",
    "    * n is the number of nodes in the network\n",
    "    * Ridx is a vector indicating wind nodes\n",
    "    * T is the number of time steps\n",
    "    * Y is the admittance matrix (n-by-n)\n",
    "    * slack is the index of the slack bus\n",
    "    * k is the vector of generator participation factors\n",
    "    \"\"\"\n",
    "    \n",
    "    # zero out slack row of Y:\n",
    "    adm = deepcopy(Y)\n",
    "    adm[slack,:] = zeros(n)\n",
    "    \n",
    "    # remove slack column of Y:\n",
    "    adm = adm[:,setdiff(1:n,slack)]\n",
    "    \n",
    "    # A has a block diagonal pattern where each\n",
    "    # block is Atemp:\n",
    "    Atemp = zeros(n+1,2*n)\n",
    "    Atemp = [[-eye(n) adm -k]; ones(1,n) zeros(1,n-1) 1]\n",
    "    \n",
    "    # Remove columns corresponding to non-wind nodes:\n",
    "    Atemp = sparse(Atemp[:,[Ridx,n+1:2*n]])\n",
    "    \n",
    "    # Now we can tile the Atemp matrix to generate A:\n",
    "    A = Atemp\n",
    "    for t = 2:T\n",
    "        A = blkdiag(A, Atemp)\n",
    "    end\n",
    "    \n",
    "    return A\n",
    "end\n",
    "\n",
    "function tmp_inst_b(n,T,G0,D)\n",
    "    \"\"\" Generate the vector b of power balance constraints.\n",
    "    Assumes G0 and D are nT-by-1 vectors.\n",
    "    \"\"\"\n",
    "    b = FloatingPoint[]\n",
    "    \n",
    "    # b = zeros((n+1)*T,1)\n",
    "    netGen = G0 - D\n",
    "    for t = 1:T\n",
    "        start = (t-1)*n + 1\n",
    "        stop = start + n - 1\n",
    "        mismatch = -sum(netGen[start:stop])\n",
    "        append!(b,netGen[start:stop])\n",
    "        push!(b,mismatch)\n",
    "    end\n",
    "    return sparse(b)\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## 3. Line Temperature Constraint\n",
    "\n",
    "The purpose of our optimization routine is to find wind patterns that excessively heat a line of our choosing. This is expressed mathematically in terms of the angle variables:\n",
    "\n",
    "\\begin{align}\n",
    "\\begin{bmatrix} x^{(1)\\top} & \\theta^{(1)\\top} & \\alpha^{(1)} & x^{(2)\\top} & \\theta^{(2)\\top} & \\alpha^{(2)} \\end{bmatrix}\n",
    "\\underbrace{\\begin{bmatrix}\n",
    "0 & Q_\\theta^{(1)} & 0 & 0 & 0 & 0 \\\\\n",
    "0 & 0 & 0 & 0 & 0 & 0 \\\\\n",
    "0 & 0  & 0 & 0 & 0 & 0 \\\\\n",
    "0 & 0 & 0 & 0 & 0 & 0 \\\\\n",
    "0 & 0 & 0 & 0 & Q_\\theta^{(2)} & 0 \\\\\n",
    "0 & 0 & 0 & 0 & 0 & 0\\end{bmatrix}}_{Q_\\theta}\n",
    "\\begin{bmatrix} x^{(1)} \\\\ \\theta^{(1)} \\\\ \\alpha^{(1)} \\\\ x^{(2)} \\\\ \\theta^{(2)} \\\\ \\alpha^{(2)} \\end{bmatrix} &= c \\text{, where} \\\\\n",
    "Q_\\theta^{(t)} = \\tau^{T -t} (e_i - e_k)(e_i - e_k)^\\top\n",
    "\\end{align}\n",
    "\n",
    "$Q_\\theta$ has two unpleasant qualities:\n",
    "\n",
    "1. It is block diagonal rather than simply diagonal.\n",
    "2. There are several different coefficients (we would prefer for each coefficient to be 1).\n",
    "\n",
    "We would like to replace $z^\\top Q_\\theta z=c$ by the simpler quadratic constraint found in the trust region subproblem: $\\lVert \\hat{\\theta}\\rVert = c$. To this end, we introduce $T$ new variables representing scaled versions of the angle difference $(\\theta_i - \\theta_k)$ between angles at the endpoints of the chosen line. There will be one new variable per time step $t$ called $\\hat{\\theta}_{ik}^{(t)}$. This variable is defined as:\n",
    "\n",
    "\\begin{align}\n",
    "\\hat{\\theta}_{ik}^{(t)} &= \\tau^{\\frac{1}{2}(T-t)}\\left(\\theta_i^{(t)} - \\theta_k^{(t)}\\right) \\\\\n",
    "\\implies - \\tau^{\\frac{1}{2}(T-t)}(\\theta_i) + \\tau^{\\frac{1}{2}(T-t)}(\\theta_k) + \\hat{\\theta}_{ik}^{(t)} &= 0\n",
    "\\end{align}\n",
    "\n",
    "With the new variables $\\hat{\\theta}_{ik}$ defined in this way, we can replace $$z^\\top Q_\\theta z=c$$ by $$\\lVert \\hat{\\theta}_{ik}\\rVert=c~.$$ This is the norm constraint we desire.\n",
    "\n",
    "We extend the vector of variables $z$ to include these $T$ additional variables, and we augment $A$ to reflect the above relationship. The following method generates the $T$ new rows of $A$ necessary to relate angle differences to the new variables."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "collapsed": true
   },
   "outputs": [],
   "source": [
    "function tmp_inst_A_scale(n,Ridx,T,tau,slack,line)\n",
    "    \"\"\" Augment A with T additional rows relating\n",
    "    angle difference variables to angle variables.\n",
    "    \n",
    "    Returns a T-by-(n+nr+1)*T matrix that may be\n",
    "    concatenated with the output of temp_inst_A\n",
    "    \n",
    "    Arguments:    \n",
    "    * n is the number of nodes in the network\n",
    "    * Ridx is a vector indicating wind nodes\n",
    "    * T is the number of time steps\n",
    "    * tau is the thermal coefficient from IEEE 738\n",
    "    * slack is the index of the slack bus\n",
    "    * line is the pair (i,k) indicating the chosen\n",
    "    line\n",
    "    \"\"\"\n",
    "    (i,k) = line\n",
    "    nr = length(Ridx)\n",
    "    \n",
    "    A = zeros(T,(nr+n+2)*T)\n",
    "    \n",
    "    for t = 1:T\n",
    "        i_pos = (nr+n+1)*(t-1) + n + i\n",
    "        k_pos = (nr+n+1)*(t-1) + n + k\n",
    "        coef = tau^((1/2)*(T-t))\n",
    "        A[t,i_pos] = -coef\n",
    "        A[t,k_pos] = coef\n",
    "        A[t,(n+nr+1)*T + t] = 1\n",
    "    end\n",
    "    slack_cols = [(nr+n+1)*(t-1) + nr + slack for t in 1:T]\n",
    "    \n",
    "    # remove slack columns:\n",
    "    return sparse(A[:,setdiff(1:(n+nr+2)*T,slack_cols)])\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Having added $T$ new variables to $z$, we must pad $Q_x$ and $b$ with the appropriate number of zeros."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "metadata": {
    "collapsed": false,
    "scrolled": true
   },
   "outputs": [
    {
     "data": {
      "text/plain": [
       "tmp_isnt_pad_Q (generic function with 1 method)"
      ]
     },
     "execution_count": 13,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function tmp_inst_pad_b(b,T)\n",
    "    \"\"\" Append T zeros to vector b\n",
    "    \"\"\"\n",
    "    append!(b,zeros(T))\n",
    "end\n",
    "\n",
    "function tmp_isnt_pad_Q(Q,T)\n",
    "    \"\"\" Add T rows and T columns of zeros\n",
    "    to matrix Q\n",
    "    \"\"\"\n",
    "    m,n = size(Q)\n",
    "    return [[Q zeros(m,T)]; zeros(T,n+T)]\n",
    "end\n",
    "    "
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "### Computing line temp constraint parameters\n",
    "\n",
    "With the newly defined angle difference variables, the quadratic temperature constraint becomes a simple norm constraint. The new vector of variables is\n",
    "\n",
    "\\begin{align}\n",
    "\\hat{z} &= \\begin{bmatrix} x^{(1)\\top} & \\theta^{(1)\\top} & \\alpha^{(1)} & \\cdots & x^{(T)\\top} & \\theta^{(T)\\top} & \\alpha^{(T)} & | & \\hat{\\theta}_{ik}^\\top \\end{bmatrix}^\\top \\\\\n",
    "&= \\begin{bmatrix} z \\\\ \\hat{\\theta}_{ik} \\end{bmatrix}\n",
    "\\end{align}\n",
    "\n",
    "Letting $\\hat{A}$ be the extension of $A$ relating angle difference variable to angle variables $\\theta_i$ and $\\theta_k$, we obtain the following version of the temporal instanton problem:\n",
    "\n",
    "\\begin{align}\n",
    "\\min & \\hat{z}^\\top \\begin{bmatrix} Q_{obj} & 0 \\\\ 0 & 0 \\end{bmatrix} \\hat{z}\\\\\n",
    "&\\text{s.t.} \\\\\n",
    "\\begin{bmatrix} A \\\\ \\hat{A} \\end{bmatrix} \\hat{z} &= \\begin{bmatrix} b \\\\ 0 \\end{bmatrix} \\\\\n",
    "\\hat{z}^\\top \\begin{bmatrix}  0 & 0 \\\\ 0 & I \\end{bmatrix} \\hat{z} &= c \\iff \\lVert \\hat{\\theta}_{ik}\\rVert = c\n",
    "\\end{align}"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia",
   "language": "julia",
   "name": "julia"
  },
  "language_info": {
   "name": "julia",
   "version": "0.3.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 0
}
